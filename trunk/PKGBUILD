# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Justin Wilcox <nat1192 at gmail dot com>

pkgname=restbed
pkgver=4.6+104+gf74f932
pkgrel=1
pkgdesc="A framework for asynchronous RESTful functionality in C++11 applications"
arch=(x86_64)
url="https://github.com/Corvusoft/restbed"
license=(AGPL3)
depends=(openssl)
makedepends=(cmake asio)
checkdepends=(catch2)
_commit=f74f9329dac82e662c1d570b7cd72c192b729eb4
source=("${url}/archive/${_commit}/${pkgname}-${pkgver}.tar.gz"
        find-catch2.patch
        restbed-build-allow-only-static-or-shared-crypto-lib.patch::"https://github.com/aberaud/restbed/commit/a26b364e03228de27498974bb1bbebd73145d30e.patch"
        restbed-socket_impl-cb9caf20724c6c73782f3b42c580b8aec31e989a.patch::"https://github.com/aberaud/restbed/commit/cb9caf20724c6c73782f3b42c580b8aec31e989a.patch"
        restbed-socket_impl-b02cbb77923349fa45348ba7cbd0d918d4610c9c.patch::"https://github.com/aberaud/restbed/commit/b02cbb77923349fa45348ba7cbd0d918d4610c9c.patch"
        restbed-socket_impl-c88fc0aac3f041967ff35ce7f54cae222b4748da.patch::"https://github.com/aberaud/restbed/commit/c88fc0aac3f041967ff35ce7f54cae222b4748da.patch"
        restbed-service_impl-58eaf0a1df49917145357f86c87b3f1acadaa66a.patch::"https://github.com/aberaud/restbed/commit/58eaf0a1df49917145357f86c87b3f1acadaa66a.patch")
sha256sums=('77baf21ac79c1a4d2d604a546f74bb8ca1c2e1e0bc48f35839c92f2eadf75cb8'
            '7f4454a875355b4d430f9d4b3408831de27d27bd760dcd70435fcfe4e1ce3c18'
            'd1b5225f465e2a713213a59b5e0da3f17db81231ae9f1c93b67ec22fb54e02ea'
            'ae5b70fdc06e141c2d28ca65763bfe93dc587eca1a490482cf678bea95ae1d25'
            '38754ee2a042e37ebfdea8de10c8a27a74d86f9f3cfdb22326a280f05b6d1cd2'
            '6b0ac759efb65d3ac741aa88a90ae647bb92c8235f7a5d66a2c453fa9b0e916f'
            'a7e18586592e3a19130948146fabaafcc5365ff8a82098a7e047e486696d82c8')

prepare() {
  mkdir -p build
  cd ${pkgname}-${_commit}
  # Allows to disable static builds
  patch -p1 -i ../restbed-build-allow-only-static-or-shared-crypto-lib.patch
  # Find catch2 correctly
  patch -p1 -i ../find-catch2.patch
  # Add missing include directory for tests
  sed -i -e 's/include_directories( SYSTEM /include_directories( ${INCLUDE_DIR} SYSTEM /' test/{integration,unit,regression,feature}/CMakeLists.txt
  # Force tests to use shared library, for correct dependencies and linking
  sed -i -e '/^target_link_libraries/ s#${STATIC_LIBRARY_NAME}#restbed-shared#' test/{integration,unit,regression,feature}/CMakeLists.txt
  # Patches required by Ring/Jami
  patch -p1 -i ../restbed-socket_impl-cb9caf20724c6c73782f3b42c580b8aec31e989a.patch
  patch -p1 -i ../restbed-socket_impl-b02cbb77923349fa45348ba7cbd0d918d4610c9c.patch
  patch -p1 -i ../restbed-socket_impl-c88fc0aac3f041967ff35ce7f54cae222b4748da.patch
  patch -p1 -i ../restbed-service_impl-58eaf0a1df49917145357f86c87b3f1acadaa66a.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_commit} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_STATIC=OFF \
    -DBUILD_SHARED=ON \
    -DBUILD_SSL=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
