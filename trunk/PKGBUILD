# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=man-pages-de
pkgver=2.2
pkgrel=1
pkgdesc="German Linux man pages"
arch=('any')
url="https://alioth.debian.org/projects/manpages-de/"
license=('GPL3')
depends=('man-db')
makedepends=('po4a')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://manpages-de.alioth.debian.org/downloads/manpages-de-$pkgver.tar.xz)
sha256sums=('7ddd7c31159c2fa06b62a9b3b626ccddddc8617fab075643560a00c62be46afa')

build() {
  cd "${srcdir}"/manpages-de-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/manpages-de-$pkgver

  make DESTDIR="${pkgdir}" install

#duplicates
rm "${pkgdir}"/usr/share/man/de/man1/groups.1.gz #shadow
}
