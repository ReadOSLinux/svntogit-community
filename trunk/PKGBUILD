# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Justin Wilcox <nat1192 at gmail dot com>
pkgname=restbed
pkgver=4.6+24+gb1f0182
pkgrel=2
pkgdesc="A framework for asynchronous RESTful functionality in C++11 applications"
arch=('x86_64')
url="https://github.com/Corvusoft/restbed"
license=('AGPL3')
depends=('openssl')
replaces=('restbed-latest')
conflicts=('restbed-latest')
makedepends=('cmake' 'asio' 'kashmir')
checkdepends=('catch2')
_commit=b1f0182c0ea74ea5e6dc08fd540462422fd0dc7f
source=("https://github.com/Corvusoft/restbed/archive/${_commit}/$pkgname-$pkgver.tar.gz"
        "fix-cmake-catch-include.patch")
sha256sums=('fb7ee67eb36eb1297e18f6ada4ba3a6fa255a6c64ff4e70a1efef5ef76c484c9'
            '9f0de7baf90cb00c02ba27fff893a4d343c46a729f85817871b71cc8f3533790')

prepare() {
  cd "$srcdir/$pkgname-$_commit"
  patch -p1 < "$srcdir/fix-cmake-catch-include.patch"
}

build() {
  cd "$srcdir/$pkgname-$_commit"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED=on \
    -DBUILD_SSL=on \
    -DBUILD_TESTS=on
  make
}

check() {
  cd "$srcdir/$pkgname-$_commit/build"
  make test
}

package() {
  cd "$srcdir/$pkgname-$_commit"

  cd build/
  make DESTDIR="$pkgdir" install
}
