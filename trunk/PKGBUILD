# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=man-pages-de
pkgver=2.9
pkgrel=1
pkgdesc="German Linux man pages"
arch=('any')
url="https://salsa.debian.org/manpages-de-team/manpages-de"
license=('GPL3')
depends=('man-db')
makedepends=('po4a')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(https://salsa.debian.org/manpages-de-team/manpages-de/-/archive/v$pkgver/manpages-de-v${pkgver}.tar.bz2)
sha256sums=('a259496d5e80db40bba173671706dae14adb516b26a89672819541645b4705b3')

build() {
  cd "${srcdir}"/manpages-de-v$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/manpages-de-v$pkgver

  make DESTDIR="${pkgdir}" install

#duplicate page included in the shadow package
rm "${pkgdir}"/usr/share/man/de/man1/groups.1.gz
}
