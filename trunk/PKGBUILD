# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Justin Wilcox <nat1192 at gmail dot com>
pkgname=restbed
pkgver=4.6+89+ga1e9c97
pkgrel=1
pkgdesc="A framework for asynchronous RESTful functionality in C++11 applications"
arch=('x86_64')
url="https://github.com/Corvusoft/restbed"
license=('AGPL3')
depends=('openssl')
replaces=('restbed-latest')
conflicts=('restbed-latest')
makedepends=('cmake' 'asio')
checkdepends=('catch2')
_commit=a1e9c970cf0886ab9e115f776f7d3c5dab52b964
source=("https://github.com/Corvusoft/restbed/archive/${_commit}/$pkgname-$pkgver.tar.gz"
        "0001-build-Only-run-tests-if-catch2-is-installed.patch"
        "0002-build-make-static-shared-builds-optional.patch"
        "0003-build-allow-only-static-or-shared-crypto-lib.patch")
sha256sums=('65c0bd66073b2646bd42b7b254820f55f8ac2ae0d1298fa64f59e104e503196e'
            '6ec39ca415b7f09246238fb9c43664caeec71601d55545f529401e7fcedc175c'
            'aaf94a7c0ceb94b1dbd3cd7dfe4541a99c107af5a528353615ff792b1f591673'
            'ca6ddb1bc8a8a623d37351b418a8c897046cfb915f7760060f1f0cfe12a2c92f')

prepare() {
  cd "$srcdir/$pkgname-$_commit"
  # Allows to disable static builds
  patch -p1 < "$srcdir/0001-build-Only-run-tests-if-catch2-is-installed.patch"
  patch -p1 < "$srcdir/0002-build-make-static-shared-builds-optional.patch"
  patch -p1 < "$srcdir/0003-build-allow-only-static-or-shared-crypto-lib.patch"
  # Add missing include directory for tests
  sed -i -e 's/include_directories( SYSTEM /include_directories( ${INCLUDE_DIR} SYSTEM /' test/{integration,unit,regression,feature}/CMakeLists.txt
  # Force tests to use shared library, for correct dependencies and linking
  sed -i -e '/^target_link_libraries/ s#${STATIC_LIBRARY_NAME}#restbed-shared#' test/{integration,unit,regression,feature}/CMakeLists.txt
}

build() {
  cd "$srcdir/$pkgname-$_commit"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_STATIC=OFF \
    -DBUILD_SHARED=ON \
    -DBUILD_SSL=on
  make
}

check() {
  cd "$srcdir/$pkgname-$_commit/build"
  make test
}

package() {
  cd "$srcdir/$pkgname-$_commit"

  cd build/
  make DESTDIR="$pkgdir" install
}
