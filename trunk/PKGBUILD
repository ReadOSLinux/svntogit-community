# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: TingPing <tingping@tingping.se>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgbase=brotli
pkgname=('brotli' 'python-brotli' 'python2-brotli' 'brotli-testdata')
pkgver=1.0.6
pkgrel=1
pkgdesc='Brotli compression library'
arch=('x86_64')
license=('MIT')
url='https://github.com/google/brotli'
depends=('gcc-libs')
makedepends=('cmake' 'python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/$pkgbase/archive/v$pkgver.tar.gz")
sha512sums=('b9847375471de3ae815ef4bb45a29653c343fad0a891a79d5132fcdee34c85caafd82289c8b413c3ef609049f2e8c4af9f9abd1736a2408ba44544c5fefc0010')

prepare() {
  cp -a brotli-$pkgver{,-py2}
  mkdir -p build
}

build() {
  cd "$srcdir"/brotli-$pkgver
  python setup.py build

  cd "$srcdir"/brotli-$pkgver-py2
  python2 setup.py build

  cd "$srcdir"/build
  cmake ../brotli-$pkgver -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_INSTALL_LIBDIR="/usr/lib"
  make
}

check() {
  cd brotli-$pkgver
  make test
  python setup.py test
  cd "$srcdir"/brotli-$pkgver-py2
  python2 setup.py test
}

package_brotli() {
  cd build
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-brotli() {
  depends=('python')

  cd brotli-$pkgver

  python setup.py install --skip-build -O1 --root="$pkgdir"
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-brotli() {
  depends=('python2')

  cd brotli-$pkgver-py2

  python2 setup.py install --skip-build -O1 --root="$pkgdir"
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_brotli-testdata() {
  depends=()

  cd brotli-$pkgver
  install -dm755 "$pkgdir"/usr/share/brotli
  cp -a tests/testdata "$pkgdir"/usr/share/brotli/
  install -D -m644 "$srcdir"/brotli-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
