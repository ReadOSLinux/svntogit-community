# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=man-pages-de
pkgver=2.13
pkgrel=1
pkgdesc="German Linux man pages"
arch=('any')
url="https://salsa.debian.org/manpages-de-team/manpages-de"
license=('GPL3')
depends=('man-db')
makedepends=('po4a')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(https://salsa.debian.org/manpages-de-team/manpages-de/-/archive/v$pkgver/manpages-de-v${pkgver}.tar.bz2)
sha256sums=('175ad7d3ce336aa52a431d4e6599156da4c7f8e17579f4a7a7f776d52eb1ec37')

build() {
  cd "${srcdir}"/manpages-de-v$pkgver

  ./configure --prefix=/usr --enable-distribution=archlinux
  make
}

package() {
  cd "${srcdir}"/manpages-de-v$pkgver

  make DESTDIR="${pkgdir}" install
}
