# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Antoine Lubineau <antoine@lubignon.info>
# Contributor: Leopold Bloom <blinxwang@gmail.com>
# Contributor: Michal Krenek (a.k.a. Mikos) <m.krenek@gmail.com>

pkgname=beignet
pkgver=1.3.1
pkgrel=2
pkgdesc="An open source OpenCL implementation for Intel IvyBridge+ iGPUs"
arch=('x86_64')
url="https://01.org/${pkgname}"
license=('LGPL')
depends=('glu' 'llvm' 'mesa' 'ocl-icd' 'opencl-headers')
makedepends=('clang' 'cmake' 'python')
provides=('opencl-intel')
conflicts=('opencl-intel')
source=("https://01.org/sites/default/files/${pkgname}-${pkgver}-source.tar.gz"
        '0001-Backend-Remove-old-llvm-support-code.patch'
        '0002-Backend-Fix-an-include-file-error-problem.patch'
        '0003-Backend-Refine-LLVM-version-check-macro.patch'
        '0004-Backend-Add-LLVM40-support.patch')
sha256sums=('399b7bc64d674b3092683263f7c085c5592686b9d837817e77857248e5cb561f'
            'f6b785f887aae72da09af71cf0e5ffc4609cb560347aee80f201699f8bbe86d7'
            'ac16a8ecc0ae0ebc1073ffc101315e74deda8f8025918427e9e86a3fc6a71851'
            'f3d681efdb6b2c390823ff91a6fed02742e556da6dbb43fef41240d1d6d5ee07'
            'a0d3aebf0d24a8dd4939833b8629947c813338e45dc7dfcd277fafdb10f093e5')

prepare() {
    cd ${pkgname^}-${pkgver}-Source
    mkdir -p build

    # LLVM 4.0.0 support
    patch -Np1 -i ../0001-Backend-Remove-old-llvm-support-code.patch
    patch -Np1 -i ../0002-Backend-Fix-an-include-file-error-problem.patch
    patch -Np1 -i ../0003-Backend-Refine-LLVM-version-check-macro.patch
    patch -Np1 -i ../0004-Backend-Add-LLVM40-support.patch
}

build() {
    cd ${pkgname^}-${pkgver}-Source/build

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_BUILD_TYPE=RELEASE
    make
}

package() {
    cd ${pkgname^}-${pkgver}-Source/build

    make DESTDIR="${pkgdir}" install

    # Remove headers already provided by 'opencl-headers'
    cd "${pkgdir}/usr/include/CL"
    rm cl.h cl_egl.h cl_ext.h cl_gl.h cl_gl_ext.h cl_platform.h opencl.h
}
